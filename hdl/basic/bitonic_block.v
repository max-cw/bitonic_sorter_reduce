`timescale 1ns / 1ps
//////////////////////////////////////////////////////////////////////////////////
// Company: 
// Engineer: Chaowaroj (Max) Wanotayaroj (https://github.com/max-cw)
// 
// Create Date: 10.02.2018 13:22:07
// Design Name: 
// Module Name: bitonic_block
// Project Name: bitonic_sort
// Target Devices:
// Tool Versions:
// Description:
// Dependencies:
// Revision:
// Revision 0.01 - File Created
// Additional Comments:
// License: MIT
//  Copyright (c) 2026 Chaowaroj Wanotayaroj
//  Copyright (c) 2019 Dmitry Matyunin
//  Permission is hereby granted, free of charge, to any person obtaining a copy
//  of this software and associated documentation files (the "Software"), to deal
//  in the Software without restriction, including without limitation the rights
//  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
//  copies of the Software, and to permit persons to whom the Software is
//  furnished to do so, subject to the following conditions:
//
//  The above copyright notice and this permission notice shall be included in
//  all copies or substantial portions of the Software.
//
//  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
//  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY
//  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
//  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
//  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
//  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
//  THE SOFTWARE.
// 
//////////////////////////////////////////////////////////////////////////////////

module bitonic_block #(
	parameter DATA_WIDTH = 16,
	parameter KEY_WIDTH = DATA_WIDTH, //Restrict comparator to KEY_WIDTH MSB's
	parameter ORDER = 0,
	parameter POLARITY = 0,
	parameter DIR = 0,
	parameter SIGNED = 0,
	parameter PIPE_REG = 1,
	parameter MAXOUT_NUM = 2**(ORDER+1),
	parameter IN_NUM = 2**(ORDER+1) > 2*MAXOUT_NUM ? 2*MAXOUT_NUM : 2**(ORDER+1),
	parameter OUT_NUM = 2**(ORDER+1) > MAXOUT_NUM ? MAXOUT_NUM : 2**(ORDER+1)

)
(
	input wire clk,
	input wire [DATA_WIDTH*2**(ORDER+1)-1:0]data_in,
	output wire [DATA_WIDTH*2**(ORDER+1)-1:0]data_out
);

localparam STAGES = ORDER+1;
localparam STAGE_DATA_WIDTH = DATA_WIDTH*2**(ORDER+1);
localparam STAGE_IN_DATA_WIDTH = DATA_WIDTH*IN_NUM;
localparam STAGE_OUT_DATA_WIDTH = DATA_WIDTH*OUT_NUM;

function integer index(input integer SS, input integer BS);
	integer i, j, ind;
begin
	ind = 0;
	for (i = 0; i < SS; i = i + 1) begin
		for (j = i+1; j < SS+1; j = j + 1) begin
			ind = ind + 1;
		end
	end
	index = ind + BS + 1;
end
endfunction

wire [DATA_WIDTH*2**(ORDER+1)-1:0]stage_data[STAGES:0];

assign stage_data[0] = data_in;
assign data_out = stage_data[STAGES];

genvar stage;
genvar node;

generate for (stage = 0; stage < STAGES; stage = stage + 1) begin: BLOCK_STAGE
	localparam NODES_FULL = 2**stage;
	localparam NODE_ORDER = STAGES - stage - 1;
	localparam NODE_OUT_NUM = 2**(NODE_ORDER+1);
	localparam NODES = $rtoi($ceil($itor(OUT_NUM) / NODE_OUT_NUM));
	localparam NODE_SKIP = NODE_OUT_NUM >= 4*MAXOUT_NUM;
		
	wire [STAGE_DATA_WIDTH-1:0]stage_data_in;
	wire [STAGE_DATA_WIDTH-1:0]stage_data_out;
		
	assign stage_data_in = stage_data[stage];
	assign stage_data[stage + 1] = 	stage_data_out;

	for (node = 0; node < NODES_FULL; node = node + 1) begin: NODE
		localparam NODE_REMOVE = POLARITY==0 ? node >= NODES : node < (NODES_FULL-NODES);
		localparam INDEX = index(ORDER, stage);
		//Same as REGOUT_EN for node module. Use to pipeline match when SKIP/REMOVE nodes
		localparam NODEREG_EN = ((PIPE_REG == 0) ? 0 : ((INDEX % PIPE_REG) == 0)) && (NODE_SKIP==1 || NODE_REMOVE==1);
		localparam NODE_DATA_WIDTH = DATA_WIDTH*NODE_OUT_NUM;
		wire [NODE_DATA_WIDTH-1:0]node_data_in;
		reg  [NODE_DATA_WIDTH-1:0]node_data_out;
		wire  [NODE_DATA_WIDTH-1:0]node_data_temp;
			
		assign node_data_in = stage_data_in[NODE_DATA_WIDTH*(node + 1)-1-:NODE_DATA_WIDTH];
		assign stage_data_out[NODE_DATA_WIDTH*(node + 1)-1-:NODE_DATA_WIDTH] = node_data_out;
			
		if (NODE_SKIP) begin
			//Input > 2*MAXOUT_NUM. Skip the comparison node and
			//instead move the top candidates from each half

			//Next stage output number per node. Should be half the current one
			localparam NODE_OUT_NUM_NEXT = 2**(NODE_ORDER);
			if (POLARITY==0) begin
			assign 	node_data_temp[0 +: DATA_WIDTH*NODE_OUT_NUM_NEXT/2]
					= node_data_in[0 +: DATA_WIDTH*NODE_OUT_NUM_NEXT/2];
			assign 	node_data_temp[DATA_WIDTH*NODE_OUT_NUM_NEXT-1 -: DATA_WIDTH*NODE_OUT_NUM_NEXT/2]
					= node_data_in[NODE_DATA_WIDTH-1 -: DATA_WIDTH*NODE_OUT_NUM_NEXT/2];
			assign node_data_temp[DATA_WIDTH*NODE_OUT_NUM_NEXT +: DATA_WIDTH*NODE_OUT_NUM_NEXT]
					= {DATA_WIDTH*NODE_OUT_NUM_NEXT{1'bz}};
			end
			else begin
				assign node_data_temp[0 +: DATA_WIDTH*NODE_OUT_NUM_NEXT]
						= {DATA_WIDTH*NODE_OUT_NUM_NEXT{1'bz}};
				assign 	node_data_temp[DATA_WIDTH*NODE_OUT_NUM_NEXT +: DATA_WIDTH*NODE_OUT_NUM_NEXT/2]
						= node_data_in[0+: DATA_WIDTH*NODE_OUT_NUM_NEXT/2];
				assign 	node_data_temp[NODE_DATA_WIDTH-1 -: DATA_WIDTH*NODE_OUT_NUM_NEXT/2]
						= node_data_in[NODE_DATA_WIDTH-1 -: DATA_WIDTH*NODE_OUT_NUM_NEXT/2];
			end
		end
		else if (NODE_REMOVE) begin
			//Not efficient if MAXOUT_NUM is not an exact power of 2. In that case,
			//some comparators from the "partially used" node can be removed.
			assign node_data_temp = node_data_in;
		end
		else begin
			bitonic_node #(
				.DATA_WIDTH(DATA_WIDTH),
				.KEY_WIDTH(KEY_WIDTH),
				.ORDER(NODE_ORDER),
				.DIR(DIR),
				.SIGNED(SIGNED),
				.PIPE_REG(PIPE_REG),
				.INDEX(INDEX)
			) bitonic_node_inst (
				.clk(clk),
				.data_in(node_data_in),
				.data_out(node_data_temp)
			);
		end

		if (NODEREG_EN) begin
			always @(posedge clk) node_data_out <= node_data_temp;
		end
		else always @(*) node_data_out = node_data_temp;
	end
end endgenerate

endmodule
